#!/bin/csh -f
#
# pget:   get exactly one file from the msas1 at navo.
#
# Usage:  pget f1 f2
#
#         f1   is a file on the msas1
#         f2   is where to copy it to
#
#         f1 may use a local unix path 
#            (~user/file or /scr/user/file for /u/b/user/file)
#         f2 may be a directory
#
# Uses rcp to msas1-hip0.
# Finally uses rsh to dmput the msas1 file back to tape.
# Note that rcp and rsh require an appropriate .rhosts on msas1.
#
# Alan J. Wallcraft,  NRL,  June 1997.
#
#set echo

if ($#argv != 2) then
    echo "Usage:  pget f1 f2"
    exit 1
endif

#
# f1 can use a unix path
#
set f1b=`basename $1`
set f1d=`dirname $1`
if (-d $f1d) then
  set f1u=`cd $f1d ; pwd`/$f1b
else
  set f1u=$1
endif
set f1=`echo $f1u | sed -e 's?^/u/home/?/u/b/?'  -e 's?^/scr/?/u/b/?'`

#
# f2 can be a directory
#
if (-d $2) then
  set f2=${2}/$f1b
else
  set f2=$2
endif

#
# --- try msas1-hip0.
#
/usr/bin/rsh msas1-hip0 -n /usr/bin/dmget ${f1}
#
echo rcp msas1-hip0:${f1} ${f2}
/usr/bin/rcp msas1-hip0:${f1} ${f2}
#
if ($status == 0) then
#
# --- force the msas1 file back to tape.
#
   nohup /usr/bin/rsh msas1-hip0 -n /usr/bin/dmput -r ${f1} >& /dev/null &
else
#
# --- msas1-hip0 failed, try msas1.
#
   /usr/bin/rsh msas1 -n /usr/bin/dmget ${f1}
#
   echo rcp msas1:${f1} ${f2}
   /usr/bin/rcp msas1:${f1} ${f2}
   nohup /usr/bin/rsh msas1 -n /usr/bin/dmput -r ${f1} >& /dev/null &
endif
